plugins {
	id 'de.fntsoftware.gradle.markdown-to-pdf' version '1.0.1'
	id 'java'
	id 'application'

	// we don't want to cause conflicts between our dependencies and the target application
	// since the agent will be loaded with the same class loader as the profiled application
	// so we use the shadow plugin to relocate our dependencies
	id 'com.github.johnrengelman.shadow' version '4.0.3'
	id 'maven'
	id 'jacoco'
}

version appVersion

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

ext {
	outputVersion = rootProject.appVersion + '-jacoco-' + jacocoVersion
}

jacocoTestReport {
	reports {
		xml.enabled true
	}
}
tasks.test.finalizedBy(jacocoTestReport)

dependencies {
	implementation project(':teamscale-client')
	implementation project(':report-generator')

	implementation "org.jacoco:org.jacoco.core:$jacocoVersion"
	implementation "org.jacoco:org.jacoco.report:$jacocoVersion"
	implementation "org.jacoco:org.jacoco.agent:$jacocoVersion:runtime"

	implementation 'ch.qos.logback:logback-core:1.2.3'
	implementation 'ch.qos.logback:logback-classic:1.2.3'

	implementation 'io.reactivex.rxjava2:rxjava:2.1.2'
	implementation 'com.beust:jcommander:1.71'
	implementation 'org.conqat:org.conqat.lib.commons:0.20190410'

	implementation 'com.squareup.retrofit2:retrofit:2.4.0'
	implementation 'com.sparkjava:spark-core:2.7.2'

	implementation 'com.squareup.moshi:moshi:1.8.0'

	testImplementation 'junit:junit:4.12'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
	testImplementation 'org.assertj:assertj-core:3.8.0'
	testImplementation 'org.mockito:mockito-core:2.19.0'
	testImplementation project(':tia-client')
}

test {
	testLogging.exceptionFormat "full"
}

mainClassName = 'com.teamscale.jacoco.agent.Main'

shadowJar {
	// Needed as a workaround for https://github.com/johnrengelman/shadow/issues/521
	inputs.property("debug", project.hasProperty("debug"))

	baseName = 'teamscale-jacoco-agent'
	classifier = null
	// since this is used as an agent, we want it to always have the same name
	// otherwise people have to adjust their -javaagent parameters after every
	// update
	version = null
	manifest {
		attributes 'Premain-Class': 'com.teamscale.jacoco.agent.PreMain'

		// The jaxb library, which we are shading is a multi release jar, so we have to explicitly "inherit" this attribute
		attributes 'Multi-Release': 'true'
	}

	// Fix relocation of multi version jar
	// Default would be shadow/META-INF/versions/9/, which is wrong
	// See http://openjdk.java.net/jeps/238
	relocate 'META-INF/versions/9/', 'META-INF/versions/9/shadow/'

	mergeServiceFiles()
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
	target = tasks.shadowJar
	onlyIf {
		!project.hasProperty("debug")
	}
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

startShadowScripts {
	applicationName = 'convert'
}

distributions {
	shadow {
		distributionBaseName = 'teamscale-jacoco-agent'
		contents {
			from(readmeToPdf) {
				into 'documentation'
				rename 'README.pdf', 'userguide.pdf'
			}

			filesMatching('**/VERSION.txt') {
				filter {
					it.replace('%APP_VERSION_TOKEN_REPLACED_DURING_BUILD%', outputVersion)
				}
			}
		}
	}
}

tasks.named('shadowDistZip').configure {
	archiveName = 'teamscale-jacoco-agent.zip'
}

processResources {
	filesMatching('**/app.properties') {
		filter {
			it.replace('%APP_VERSION_TOKEN_REPLACED_DURING_BUILD%', rootProject.appVersion)
		}
	}
}

install {
	repositories.mavenInstaller {
		configuration = configurations.shadow
		pom.whenConfigured { it.dependencies.clear() }
	}
}

// At the moment we are stuck with the old maven plugin until support for private key
// files is added or we add a dedicated user with a password to our server.
// https://github.com/gradle/gradle/issues/1263
uploadShadow {
	repositories {
		mavenDeployer {
			configuration = configurations.deployerJars
			repository(url: cqseMavenRepositoryUrl) {
				authentication(userName: cqseMavenRepositoryUser, privateKey: cqseMavenRepositoryPrivateKey)
			}
			pom.whenConfigured { it.dependencies.clear() }
		}
	}
}
