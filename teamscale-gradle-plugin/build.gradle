plugins {
	id 'org.jetbrains.kotlin.jvm' version '1.4.31'
	id 'com.teamscale.java-convention'
	id 'java-gradle-plugin'
	id 'com.teamscale.coverage'
	id 'com.teamscale.publish'
	id "com.gradle.plugin-publish"
}

publishAs {
	readableName = "Teamscale Gradle Plugin"
	description = "A Gradle plugin that supports collecting Testwise Coverage and uploading reports to Teamscale."
}

gradlePlugin {
	plugins {
		teamscalePlugin {
			id = "com.teamscale"
			implementationClass = "com.teamscale.TeamscalePlugin"
		}
	}
}

dependencies {
	implementation project(':teamscale-client')
	implementation project(':report-generator')
	implementation gradleApi()
	implementation 'org.eclipse.jgit:org.eclipse.jgit:5.11.1.202105131744-r'
	implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
	testImplementation "com.squareup.okio:okio:2.10.0"
}

processResources {
	inputs.property("version", version)
	filesMatching('**/plugin.properties') {
		filter {
			it.replace('%PLUGIN_VERSION_TOKEN_REPLACED_DURING_BUILD%', version)
		}
	}
}

test {
	dependsOn ':agent:publishToMavenLocal'
	dependsOn ':impacted-test-engine:publishToMavenLocal'
	dependsOn ':teamscale-client:publishToMavenLocal'
	dependsOn ':tia-client:publishToMavenLocal'
	dependsOn ':report-generator:publishToMavenLocal'
}

pluginBundle {
	website = 'https://www.teamscale.com/'
	vcsUrl = 'https://github.com/cqse/teamscale-jacoco-agent'
	description = 'Supports collecting Testwise Coverage and uploading reports to Teamscale.'
	tags = ['teamscale', 'coverage', 'tga', 'test', 'gap', 'junit', 'upload']

	plugins {
		teamscalePlugin {
			displayName = 'Teamscale Gradle plugin'
		}
	}
}