buildscript {
	repositories {
		mavenCentral()
		mavenLocal()
	}
}

plugins {
	id 'java'
	id 'jacoco'
	id 'com.teamscale'
}

import com.teamscale.TestImpacted

ext.junit4Version = '4.13.2'
ext.junitVintageVersion = '5.10.1'
ext.junitPlatformVersion = '1.4.0'
ext.junitJupiterVersion = '5.10.1'

if (!project.hasProperty("withoutServerConfig")) {
	teamscale {
		server {
			url = 'http://127.0.0.1:64000'
			userName = 'build'
			userAccessToken = '82l1jtkIx6xG7DDG34FLsKhejcHz1cMu' // Not a real access token
			project = 'test'
		}
	}
}

teamscale {
	commit {
		timestamp = 1544512967526L
		branchName = "master"
	}
	report {
		testwiseCoverage {
			partition = 'Unit Tests'
		}
	}
}

task unitTest(type: TestImpacted) {
	useJUnitPlatform {
		excludeTags 'integration'
	}
	jacoco.includes = [ project.findProperty('jacocoIncludePattern') ?: 'com.example.project.*' ]
	testLogging {
		// events "started", "skipped", "failed"
		exceptionFormat "short"
		afterSuite { desc, result ->
			if (!desc.parent) { // will match the outermost suite
				def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
				def startItem = '|  ', endItem = '  |'
				def repeatLength = startItem.length() + output.length() + endItem.length()
				println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
			}
		}
	}
	if (project.hasProperty('excludeFailingTests')) {
		exclude '**/FailingRepeatedTest*'
	}
}

task integrationTest(type: TestImpacted) {
	useJUnitPlatform {
		includeTags 'integration'
	}

	jacoco.includes = [ 'com.example.project.*' ]
	teamscale.report.partition = 'Integration Tests'
}

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	// JUnit Jupiter API and TestEngine implementation
	testImplementation("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
	testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
	testImplementation("org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}")

	// If you also want to support JUnit 3 and JUnit 4 tests
	testImplementation("junit:junit:${junit4Version}")
	testRuntimeOnly("org.junit.vintage:junit-vintage-engine:${junitVintageVersion}")
}


