// Needed to make git properties work with Java 8,
// see https://github.com/n0mer/gradle-git-properties/issues/195#issuecomment-982326268
buildscript {
	dependencies {
		classpath ('org.eclipse.jgit:org.eclipse.jgit:') {
			version {
				strictly '5.13.0.202109080827-r'
			}
		}
	}
}

plugins {
	id 'com.teamscale.java-convention'
	id 'application'
	id 'com.teamscale.coverage'
	id "com.gorylenko.gradle-git-properties" version "2.4.1"
}

version 'unspecified'

jar {
	manifest {
		attributes 'Main-Class': 'Main'
	}
	// make it a fat jar
	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
}

dependencies {
	// this logback version is the oldest one available that I could get to work and possibly incompatible
	// with the one used in the agent. This way, we can test if the shadowing works correctly
	implementation 'ch.qos.logback:logback-core:1.0.0'
	implementation 'ch.qos.logback:logback-classic:1.0.0'
}
