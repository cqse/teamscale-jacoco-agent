import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
	id 'de.fntsoftware.gradle.markdown-to-pdf' version '1.0.1'
	id 'java'
	id 'application'
	id 'com.github.johnrengelman.shadow' version '4.0.1'
}

dependencies {
	compile 'org.conqat:org.conqat.lib.commons:0.20160822'
	compile 'org.apache.commons:commons-lang3:3.8.1'
	compile 'org.apache.commons:commons-exec:1.3'
}

mainClassName = 'eu.cqse.teamscale.jacoco.javaws.Main'

shadowJar {
	baseName = 'teamscale-javaws-wrapper'
	classifier = null
	version = rootProject.appVersion
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
	target = tasks.shadowJar
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

startShadowScripts {
	applicationName = 'javaws'
}

distributions {
	shadow {
		baseName = 'javaws'
		contents {
			from(readmeToPdf) {
				into 'documentation'
				rename 'README.pdf', 'userguide.pdf'
			}

			from(project(':agent').tasks.readmeToPdf) {
				rename('README.pdf', 'agent-userguide.pdf')
				into 'documentation'
			}

			from(project(':agent').tasks.shadowJar) {
				rename(/.*/, 'agent.jar')
			}
		}
	}
}